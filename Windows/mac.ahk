;================================================================
;auto hot key script for windows
;macOS like keymaps
;================================================================
InstallKeybdHook
#UseHook

;================================================================
;関数
;================================================================

;----------------------------------------------------------------
;キーバインドを無効にするウィンドウかどうか判断する
; ahk_classはauto hot key付属のAU3_Spy.exeを使用して調べる。
;  引数 なし
;  戻り値 1:キーバインドを無効にするウィンドウ
;         0:キーバインドを有効にするウィンドウ
;----------------------------------------------------------------
is_disable_window()
{
	;tera term
	if WinActive("ahk_class VTWin32")
	{
		return 1
	}
	;vmware player 
	if WinActive("ahk_class VMPlayerFrame")
	{
		return 1
	}
	
	return 0
}

;----------------------------------------------------------------
;キーを送信する
; キーバインドを無効にするウィンドウでは、送信されたキーをそのまま使用する
; キーバインドを有効にするウィンドウでは、送信されたキーを置き換える
;
;  引数 original_key:キーバインドを無効にするウィンドウの場合、送信するキー
;       replace_key:キーバインドを有効にするウィンドウの場合、送信するキー
;  戻り値 なし
;----------------------------------------------------------------
send_key(original_key,replace_key)
{
	if (is_disable_window())
	{
		Send(original_key)
		return
	}
	Send(replace_key)
	return
}

;================================================================
;ctrl keymaps
;================================================================

<^n::send_key("^n","{Down}")
<^p::send_key("^p","{Up}")
<^f::send_key("^f","{Right}")
<^b::send_key("^b","{Left}")
<^a::send_key("^a","{Home}")
<^e::send_key("^e","{End}")

<^h::send_key("^h","{BS}")
<^d::send_key("^d","{Del}")
<^k::send_key("^k","+{End}{Del}")

;================================================================
;alt keymaps
;================================================================

<!a::send_key("!a", "^a")
<!z::send_key("!z", "^z")
<!x::send_key("!x", "^x")
<!c::send_key("!c", "^c")
<!v::send_key("!v", "^v")
<!f::send_key("!f", "^f")
